#!/usr/bin/env fish

if test "$argv[1]" = help
    echo 'Usage: cse [command]'
    echo
    echo 'Commands:'
    echo '    help      Print this help message'
    echo '    update    Update this script'
    echo '    local     Print the local directory'
    echo '    remote    Print the remote directory'
    echo '    pull      Pull changes from the CSE server to the local machine'
    echo '    push      Push changes from the local machine to the CSE server'
    echo '    give      Shortcut for give'
    echo '    test      Autotest assignments against self-made test cases'
    echo '    style     Check for style issues within a program'
    echo
    echo 'If no command is specified, SSH into the CSE server.'
    echo 'If an unrecognised command is specified, SSH into the CSE server and execute the command.'

    exit
end

set -l src (dirname (realpath (status filename)))

if test "$argv[1]" = update
    echo 'Updating...'

    cd $src || exit 1
    git pull
    ./install.fish

    exit
end

function err -a msg
    set_color red
    echo $msg >&2
    set_color normal
end

function invalid-zid -a zid
    test (string length $zid) != 8 -o (string sub -e 1 $zid) != z
end

function get-zid
    read -lp "echo -n 'Enter your zID: '" zid
    if invalid-zid "$zid"
        err "Invalid zID: $zid"
        get-zid
    else
        echo $zid
    end
end

function pull -a login from to
    rsync -L -a --delete -r -P $login:$from/ $to >&2
end

function push -a login from to
    rsync -a --delete -r -P $from/ $login:$to >&2
end

function cse-ssh -a login local_dir remote_dir
    set -l args $argv[4..]
    set -l relative_dir (realpath --relative-to $local_dir (pwd))
    string match -qr '\.\./.*' $relative_dir || set -l in_synced_dir  # If path starts with ../ it is outside the synced dir

    # SSH into CSE server
    if test -z "$args"
        # If the current directory is in the local directory, push before ssh
        if set -q in_synced_dir
            push $login $local_dir $remote_dir
            ssh -qt $login "cd $remote_dir/$relative_dir ; exec \$SHELL -l"
        else
            ssh -q $login
        end
    else
        # If args are provided, execute them in the remote server
        if set -q in_synced_dir
            push $login $local_dir $remote_dir
            ssh -qt $login "cd $remote_dir/$relative_dir && $args"
        else
            ssh -qt $login $args
        end
    end
end

set -q XDG_CONFIG_HOME && set -l config $XDG_CONFIG_HOME || set -l config $HOME/.config
set -l local_dir $HOME/Desktop/CSE
set -l remote_dir ./Desktop
set -q CSE_COURSE && set -l course $CSE_COURSE || set -l course 1511

# Print local dir
if test "$argv[1]" = local
    echo $local_dir
    exit
end

# Print remote dir
if test "$argv[1]" = remote
    echo $remote_dir
    exit
end

# Get zID from file or prompt
mkdir -p $config/cse
test -f $config/cse/zid.txt && set -l zid (cat $config/cse/zid.txt) || set -l zid (get-zid | tee $config/cse/zid.txt)
set -l login $zid@cse.unsw.edu.au

# If user cancelled zID prompt
if invalid-zid "$zid"
    err 'Invalid zID. Exiting.'
    rm $config/cse/zid.txt
    exit 1
end

# Ensure ssh keys are set up
test -f $HOME/.ssh/id_ed25519 || ssh-keygen -t ed25519 -f $HOME/.ssh/id_ed25519 -N ''
ssh-copy-id $login 2> /dev/null

# Pull changes from CSE server using rsync
if test "$argv[1]" = pull
    echo 'Pulling from remote...'
    pull $login $remote_dir $local_dir
    exit
end

# Push changes to CSE server using rsync
if test "$argv[1]" = push
    echo 'Pushing to remote...'
    push $login $local_dir $remote_dir
    exit
end

# Shortcut for give
if test "$argv[1]" = give
    set -l file $argv[3]
    set -l task $argv[2]_(basename (basename $file .c) .s)

    push $login $local_dir $remote_dir
    cse-ssh $login $local_dir $remote_dir "echo 'yes\nyes' | give cs$course $task $file"

    exit
end

# Autotest assignments
if test "$argv[1]" = test
    set -l source_path $argv[2]
    if ! test -f $source_path
        err "Invalid program: $source_path"
        exit 1
    end

    set -l prog_name (basename $argv[2] .c)
    set -l tests $src/tests/$prog_name

    if ! test -d $tests
        err "No tests found for $prog_name"
        exit 1
    end

    echo 'Starting autotest...'

    set -l exec_path /tmp/cse/$prog_name/$prog_name

    # Compile program
    mkdir -p (dirname $exec_path)
    echo "dcc --valgrind --leak-check $source_path -o $exec_path"
    dcc --valgrind --leak-check $source_path -o $exec_path

    set -l passed 0
    set -l failed 0

    for test in $tests/*
        set -l test_name (basename $test)
        set -l out_dir /tmp/cse/$prog_name/$test_name
        mkdir -p $out_dir

        # Remove carriage returns cause UNSW uses CRLF for newlines for some reason (Unix uses LF so we want LF only)
        cse-ssh $login $local_dir $remote_dir "echo '$(cat $test)' | $course $prog_name" | tr -d '\r' > $out_dir/expected
        cat $test | $exec_path > $out_dir/out
        if cmp --silent $out_dir/expected $out_dir/out
            set passed (math $passed + 1)
            printf 'Test %s (%s) - ' $test_name $prog_name
            set_color green
            echo 'passed'
            set_color normal
        else
            set failed (math $failed + 1)
            printf 'Test %s (%s) - ' $test_name $prog_name
            set_color red
            echo 'failed'
            set_color normal
            echo 'The inputs for this test were:'
            set_color red
            cat $test
            set_color normal
            echo 'The differences between your program\'s output and the expected output were:'
            diff -u $out_dir/expected $out_dir/out
        end
    end

    test $passed -eq 0 && set_color red || set_color green
    printf '%d tests passed' $passed
    test $failed -eq 0 && set_color green || set_color red
    echo " $failed tests failed"
    set_color normal

    exit
end

if test "$argv[1]" = style
    set -l source_path $argv[2]

    if ! test -f $source_path
        err "Invalid program: $source_path"
        exit 1
    end

    # Run style command
    push $login $local_dir $remote_dir
    cse-ssh $login $local_dir $remote_dir "$course style $source_path"

    echo  # Spacer

    # Find magic numbers
    # Magic numbers are defined as: any decimal or any int not between -2 and 2 or a character
    set -l magic_number_pattern '((\+|-)?([0-9]+\.[0-9]+|[0-9][0-9]+|[3-9])|\'.\')'
    # Exclusions include #defines, comments, typedefs, enums or 0.0
    set -l exclusions '^\s*(#define|//|\*|typedef|enum|([A-Z_]+\s*=))|0\.0'
    set -l magic_numbers (grep -E $magic_number_pattern $source_path | grep -vE $exclusions)

    if test -z "$magic_numbers"
        echo 'No magic numbers found!'
    else
        set -l num (echo "$magic_numbers" | grep -oE $magic_number_pattern | count)
        test $num -eq 1 || set -l plural 's'
        echo "$num magic number$plural found:"
        for line in $magic_numbers
            set -l line_with_num (grep -Fxn $line $source_path)
            set -l split (string split -m 2 ':' $line_with_num)
            set_color green
            printf 'Line %s:' $split[1]
            set_color normal
            string replace -ar $magic_number_pattern '\x1b[1;31m${1}\x1b[0m' $split[2]
        end
    end

    # Check for missing braces
    set -l missing_brace_pattern '\s*(\}\s*)?(if|else if|else|while|for)\s*\(.*\)'
    set -l missing_braces (grep -xnE $missing_brace_pattern $source_path)
    if test -z "$missing_braces"
        echo 'No conditions/loops with missing braces found!'
    else
        set -l num (count $missing_braces)
        test $num -eq 1 || set -l plural 's'
        echo "$num missing brace$plural found:"
        for line in $missing_braces
            set -l split (string split -m 2 ':' $line)
            set_color green
            printf 'Line %s:' $split[1]
            set_color normal
            echo $split[2]
        end
    end

    # Check for missing function prototypes/implementations and comments
    set -l identifier '[a-zA-Z_][a-zA-Z0-9_]*'
    set -l function_pattern "^((struct|enum)\s+)?$identifier\s+$identifier\("
    set -l func_proto_or_impls (grep -oE $function_pattern $source_path | sort -u)

    set -l missing_proto_or_impl
    set -l missing_comments

    for func in $func_proto_or_impls
        if ! string match -qr "^int\s+main\(" $func
            set -l func_name (string split '(' $func)[1]
            test (grep -c "^$func_name(" $source_path) -lt 2 && set -a missing_proto_or_impl $func_name
            test (grep -B 1 "^$func_name(" $source_path | grep -cE '^\s*(//|\*/)') -lt 1 && set -a missing_comments $func_name
        end
    end

    if test -z "$missing_proto_or_impl"
        echo 'No missing function prototypes or implementations!'
    else
        set -l num (count $missing_proto_or_impl)
        test $num -eq 1 || set -l plural 's'
        echo "Missing prototype or implementation for $num function$plural:"
        set_color red
        printf '%s\n' $missing_proto_or_impl
        set_color normal
    end

    if test -z "$missing_comments"
        echo 'No missing function comments!'
    else
        set -l num (count $missing_comments)
        test $num -eq 1 || set -l plural 's'
        echo "Missing function comment for $num function$plural:"
        set_color red
        printf '%s\n' $missing_comments
        set_color normal
    end

    exit
end

# SSH into remote and exec args if not given named args
cse-ssh $login $local_dir $remote_dir $argv
